apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply from: 'config.gradle'

// 项目构建时的脚本
buildscript {

    // 项目构建所需依赖包的仓库
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    }

    // 设置引用在构建项目时的插件
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

// Jar包基础名称
def jarBaseName = ext.project.projectName
// Jar包版本号
def jarVersion = ext.project.projectVersion
// Jar包全名
def jarFullName = jarBaseName + '-' + jarVersion + '-' + releaseTime()
// 主类
def mainClassFullName = ext.project.mainClassFullName

// 设置编译兼容性版本
sourceCompatibility = 1.8
targetCompatibility = 1.8

// 设置主类
mainClassName = mainClassFullName

// 设置编译时的编码格式
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// 设置编译时的编码
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// 设置项目的资源目录位置
sourceSets {
    main.resources.srcDirs = ["src/main/java", "src/main/resources"]
}

/**
 * 返回当前日期时间字符串
 *
 * @return 当前日期时间字符串
 */
def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}

// 生成Jar包的基本属性信息
jar {
    // 生成Jar包的属性清单
    manifest {
        attributes 'Implementation-Title': jarBaseName,
                'Implementation-Version': jarVersion,
                'Main-Class': mainClassFullName
    }

    // 编译后的字节码文件以及各种资源文件打包
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// 使用影子工具生成Jar包的基本信息
shadowJar {
    // Jar包全名
    archiveBaseName = jarFullName

    // 将依赖包中重名的spring相关文件（spring.schema等）做内容合并，避免内容被覆盖导致运行异常
    mergeServiceFiles {
        path = 'META-INF/spring.*'
    }
}

// 项目开发及运行所需依赖包的仓库
repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
}

// 项目依赖包
dependencies {
    compile("org.springframework:spring-context:$rootProject.ext.dependencies.springContextVersion")
    compile("org.springframework:spring-test:$rootProject.ext.dependencies.springTestVersion")

    compile("net.jthink:jaudiotagger:$rootProject.ext.dependencies.jAudioTaggerVersion")
    compile("net.bramp.ffmpeg:ffmpeg:$rootProject.ext.dependencies.ffmpegVersion")

    compile("com.squareup.okhttp3:okhttp:$rootProject.ext.dependencies.okHttpVersion")
    compile("com.google.code.gson:gson:$rootProject.ext.dependencies.gsonVersion")
    compile("com.google.guava:guava:$rootProject.ext.dependencies.guavaVersion")
    compile("org.apache.commons:commons-lang3:$rootProject.ext.dependencies.commonsLang3Version")
    compile("org.apache.commons:commons-collections4:$rootProject.ext.dependencies.commonsCollections4Version")
    compile("net.sf.dozer:dozer:$rootProject.ext.dependencies.dozerVersion")
    compile("org.yaml:snakeyaml:$rootProject.ext.dependencies.snakeyamlVersion")
    compile("org.slf4j:slf4j-api:$rootProject.ext.dependencies.slf4jApiVersion")
    compile("ch.qos.logback:logback-core:$rootProject.ext.dependencies.logbackCoreVersion")
    compile("ch.qos.logback:logback-classic:$rootProject.ext.dependencies.logbackClassicVersion")

    testCompile("junit:junit:$rootProject.ext.dependencies.junitVersion")
    testCompile("org.hamcrest:hamcrest-core:$rootProject.ext.dependencies.hamcrestCoreVersion")
}